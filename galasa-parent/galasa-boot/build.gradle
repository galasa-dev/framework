plugins {
    id 'galasa.framework'
}

description = 'Galasa Boot'

configurations {
    bundleDependency
    bundleDependency.transitive = false

    embedImplementation
    embedImplementation.transitive = false

    implementation.extendsFrom embedImplementation

}

version = "0.16.0"

jar {
    manifest {
        attributes "Main-Class": "dev.galasa.boot.Launcher"
    }
    into ('/bundle') {
        from configurations.bundleDependency
        // Strip version number
        rename { name ->
            def artifacts = configurations.bundleDependency.resolvedConfiguration.resolvedArtifacts
            def artifact = artifacts.find { it.file.name == name }
            "${artifact.name}.${artifact.extension}"
        }
    }
    into ('/') {
        from {
            configurations.embedImplementation.collect { it.isDirectory() ? it : zipTree(it) }
        }
    }

}

dependencies {

    implementation project (':dev.galasa.framework.maven.repository')
    embedImplementation project (':dev.galasa.framework.maven.repository.spi')

    embedImplementation 'org.apache.felix:org.apache.felix.framework:7.0.0'
    embedImplementation 'org.apache.felix:org.apache.felix.bundlerepository:2.0.2'
    implementation 'org.apache.felix:org.apache.felix.scr:2.1.14'

    embedImplementation 'commons-cli:commons-cli:1.4'
    embedImplementation 'commons-io:commons-io:2.9.0'

    bundleDependency 'org.apache.felix:org.apache.felix.bundlerepository:2.0.2'
    bundleDependency 'org.apache.felix:org.apache.felix.scr:2.1.14'
    bundleDependency 'org.apache.logging.log4j:log4j-api:2.14.1'
    bundleDependency 'org.apache.logging.log4j:log4j-core:2.14.1'
    bundleDependency project (':dev.galasa.framework.log4j2.bridge')
    bundleDependency project (':dev.galasa.framework.maven.repository')
    bundleDependency project (':dev.galasa.framework.maven.repository.spi')
    
}