plugins {
    id 'biz.aQute.bnd.builder'
    id 'galasa.api.server'
    id "org.openapi.generator" version "6.6.0"

    // testCompile requires Java plugin.
    id 'java'
}

description = 'Framework API - REST java client'

version = '0.31.0'

dependencies {
    compileOnly project(':dev.galasa.framework.api.openapi')
    implementation 'com.fasterxml.jackson.core:jackson-core:2.16.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.16.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.16.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

openApiGenerate {
    generatorName.set("java")
    inputSpec.set("$rootDir/dev.galasa.framework.api.openapi/src/main/resources/openapi.yaml")
    outputDir.set("$buildDir/generated")
    apiPackage.set("dev.galasa.framework.api.client")
    invokerPackage.set("dev.galasa.framework.api.client.invoker")
    modelPackage.set("dev.galasa.framework.api.client.model")
    configOptions.set([
      library :"native",
      openApiNullable: "false", 
      serlializationLibrary: "gson",
      hideGenerationTimestamp: "true"
    ])
    // globalProperties.set([
    //   models: "",
    //   apis: "false"
    // ])
}

tasks.named('processResources') { 
    dependsOn('openApiGenerate') 
}

sourceSets {
  main {
    java.srcDirs += "$buildDir/generated/src/main/java"
  }
}



// Note: These values are consumed by the parent build process
// They indicate which packages of functionality this OSGi bundle should be delivered inside,
// or referenced from.
// The settings here are gathered together by the build process to create a release.yaml file
// which gathers-up all the packaging metadata about all the OSGi bundles in this component.
ext.projectName=project.name
ext.includeInOBR          = true
ext.includeInMVP          = true
ext.includeInIsolated     = true
ext.includeInBOM          = true
ext.includeInCodeCoverage = true
ext.includeInJavadoc      = true

