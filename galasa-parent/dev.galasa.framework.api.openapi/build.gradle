plugins {
    id 'biz.aQute.bnd.builder'
    id 'maven-publish'
    id 'galasa.java'
    id "org.openapi.generator" version "6.6.0"
}

description = 'Galasa Open API specification'

version = "0.38.0"

repositories {
    maven {
        url "$sourceMaven"
    }
    maven {
        url "$centralMaven"
    }
}

configurations {
    conf
}

def openapiFile = layout.projectDirectory.file("src/main/resources/openapi.yaml").asFile
// Validating a single specification
openApiValidate {
    inputSpec.set(openapiFile.path)
    recommend.set(true)
}

tasks.named('processResources') { 
    dependsOn('openApiValidate') 
}

// Publish the openapi.yaml on it's own as a special artifact.
// So if someone downloads this as a dependency, they don't have to unzip anything.
def openapiYaml = artifacts.add('conf', openapiFile) {}

publishing {
    publications {
        maven(MavenPublication) {
            artifact openapiYaml
            artifactId "dev.galasa.framework.api.openapi"
            groupId 'dev.galasa'
            version "$version"
        }
    }
}


// Note: These values are consumed by the parent build process
// They indicate which packages of functionality this OSGi bundle should be delivered inside,
// or referenced from.
// The settings here are gathered together by the build process to create a release.yaml file
// which gathers-up all the packaging metadata about all the OSGi bundles in this component.
ext.projectName=project.name
ext.includeInOBR          = true
ext.includeInMVP          = true
ext.includeInIsolated     = true
ext.includeInBOM          = true
ext.includeInCodeCoverage = false
ext.includeInJavadoc      = false

